buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = "1.12.2-r152"
group = "de.ellpeck.actuallyadditions"
archivesBaseName = "ActuallyAdditions"

if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}

minecraft {
    version = "1.12.2-14.23.5.2836"
    runDir = "run"
    mappings = "stable_39"
	makeObfSourceJar = false
    replaceIn "ActuallyAdditions.java"
    replace "@VERSION@", project.version.toString()
}

repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
	maven {
		url "https://maven.tehnut.info"
	}
	maven {
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
}

dependencies {
	deobfCompile "mezz.jei:jei_1.12.2:+"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8+"
    deobfCompile ("org.cyclops.commoncapabilities:CommonCapabilities:1.12.2-+")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {

}
task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task sourcesJar(type: Jar) {
    from(sourceSets.main.allSource)
    archiveName = "${baseName}-${version}-sources.${extension}"
}

task apiJar(type: Jar) {
    from(sourceSets.main.output)
    from(sourceSets.main.java)
    include("de/ellpeck/actuallyadditions/api/**")
    archiveName = "${baseName}-${version}-api.${extension}"
}

javadoc {
    include 'de/ellpeck/actuallyadditions/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives apiJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            artifact deobfJar {
                classifier 'deobf'
            }
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact apiJar {
                classifier 'api'
            }

            pom.withXml {
                def node = asNode()
                if (node.dependencies.size() > 0)
                    node.remove(node.dependencies)
            }
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}